#
# Copyright 2021-2026 Software Radio Systems Limited
#
# By using this file, you agree to the terms and conditions set
# forth in the LICENSE file which can be found at the top level of
# the distribution.
#

resource "gitlab_pipeline_schedule" "this" {
  for_each       = var.schedules
  project        = var.ci_project_id
  description    = each.value.description
  ref            = each.value.ref
  cron           = each.value.cron
  cron_timezone  = each.value.timezone
  active         = coalesce(try(each.value.active, null), true)
  take_ownership = true
}

resource "gitlab_pipeline_schedule_variable" "vars" {
  for_each = {
    for item in flatten([
      for sched_name, s in var.schedules : [
        for k, v in coalesce(s.variables, {}) : {
          key           = "${sched_name}:${k}"
          schedule_name = sched_name
          var_key       = k
          var_value     = v
        }
      ]
    ]) : item.key => item
  }

  project              = var.ci_project_id
  pipeline_schedule_id = tonumber(split(":", gitlab_pipeline_schedule.this[each.value.schedule_name].id)[1])
  key                  = each.value.var_key
  value                = each.value.var_value
}
